# Cypher query language

Created by: Duy Phúc Trần
Created time: August 23, 2021 9:08 AM
Type: Data Science

### **Định dạng của Cypher**

- Định dạng nút (nodes)
    - **()**: nút rỗng
    - **(varname:NodeName)**: Nút có nhãn là NodeName, tên biến của nút là varname. Nút có thể không có tên biến.
    - Nên đặt tên nhãn mang tính gợi nhớ tới đối tượng, nhãn node nên là một danh từ.
    - Tên nhãn có phân biệt chữ hoa chữ thường.
- Định dạng quan hệ (relationship)
    - **[varname:RELATIONSHIP_NAME]**: mối quan hệ có nhãn là RELATIONSHIP_NAME và biến quan hệ là varname.
- Mệnh đề:
    - Ví dụ: `MATCH`, `WHERE`, `WITH`, `UNWIND`...
    - Các mệnh đề không phân biệt chữ hoa chữ thường.
- Keyword:
    - Ví dụ: `AND`, `OR`, `IN`, `NOT`, `DISTINCT`, `STARTS WITH`, `CONTAINS`, `ENDS WITH`,...
    - Các keyword không phân biệt chữ hoa chữ thường.
- Dấu chấm phẩy
    - Dùng trong trường hợp có 1 tập các câu lệnh Cypher và cần phân tách giữa các lệnh. Không nên dùng khi chỉ có 1 lệnh.

# Movie Graph

Movie graph là một graph database chứa thông tin về đạo diễn và diễn viên có liên quan đến các bộ phim mà họ đã hợp tác.

## **1. Create**

Cypher query:

Tạo bộ phim và những người có liên quan bộ phim đó:

```jsx
CREATE (TheMatrix:Movie {title:'The Matrix', released:1999, tagline:'Welcome to the Real World'})
CREATE (Keanu:Person {name:'Keanu Reeves', born:1964})
CREATE (Carrie:Person {name:'Carrie-Anne Moss', born:1967})
CREATE (Laurence:Person {name:'Laurence Fishburne', born:1961})
CREATE (Hugo:Person {name:'Hugo Weaving', born:1960})
CREATE (LillyW:Person {name:'Lilly Wachowski', born:1967})
CREATE (LanaW:Person {name:'Lana Wachowski', born:1965})
CREATE (JoelS:Person {name:'Joel Silver', born:1952})
```

Tạo mối quan hệ giữa person với movie, trong mỗi quan hệ có thể chứa thêm các thông tin khác nhau. (VD: { roles:['Neo'] })

```jsx
CREATE
(Keanu)-[:ACTED_IN {roles:['Neo']}]->(TheMatrix),
(Carrie)-[:ACTED_IN {roles:['Trinity']}]->(TheMatrix),
(Laurence)-[:ACTED_IN {roles:['Morpheus']}]->(TheMatrix),
(Hugo)-[:ACTED_IN {roles:['Agent Smith']}]->(TheMatrix),
(LillyW)-[:DIRECTED]->(TheMatrix),
(LanaW)-[:DIRECTED]->(TheMatrix),
(JoelS)-[:PRODUCED]->(TheMatrix)
```

Tương tự thêm nhiều movie và person khác.

Sau khi tạo xong ta được một graph database như sau:

![Untitled](Cypher%20query%20language%208d264c456286496594d409f32952da68/Untitled.png)

## **2.Find**

Tìm diễn viên tên là 'Tom Hanks'

```jsx
MATCH (tom {name: "Tom Hanks"}) RETURN tom
```

```
╒════════════════════════════════╕
│"tom"                           │
╞════════════════════════════════╡
│{"name":"Tom Hanks","born":1956}│
└────────────────────────────────┘
```

Tìm bộ phim tên là 'Cloud Atlas'

```jsx
MATCH (cloudAtlas {title: "Cloud Atlas"}) RETURN cloudAtlas
```

```
╒══════════════════════════════════════════════════════════════════════╕
│"cloudAtlas"                                                          │
╞══════════════════════════════════════════════════════════════════════╡
│{"tagline":"Everything is connected","title":"Cloud Atlas","released":│
│2012}                                                                 │
└──────────────────────────────────────────────────────────────────────┘
```

Tìm 10 người

```jsx
MATCH (people:Person) RETURN people.name LIMIT 10
```

```
╒════════════════════╕
│"people.name"       │
╞════════════════════╡
│"Keanu Reeves"      │
├────────────────────┤
│"Carrie-Anne Moss"  │
├────────────────────┤
│"Laurence Fishburne"│
├────────────────────┤
│"Hugo Weaving"      │
├────────────────────┤
│"Andy Wachowski"    │
├────────────────────┤
│"Lana Wachowski"    │
├────────────────────┤
│"Joel Silver"       │
├────────────────────┤
│"Emil Eifrem"       │
├────────────────────┤
│"Charlize Theron"   │
├────────────────────┤
│"Al Pacino"         │
└────────────────────┘
```

Tìm những phim được phát hành vào năm 1990

```jsx
MATCH (nineties:Movie) WHERE nineties.released >= 1990 AND nineties.released < 2000 RETURN nineties.title
```

```
╒════════════════════════╕
│"nineties.title"        │
╞════════════════════════╡
│"The Matrix"            │
├────────────────────────┤
│"The Devil's Advocate"  │
├────────────────────────┤
│"A Few Good Men"        │
├────────────────────────┤
│"As Good as It Gets"    │
.....
│"Twister"               │
├────────────────────────┤
│"Bicentennial Man"      │
├────────────────────────┤
│"A League of Their Own" │
└────────────────────────┘
```

## 3.Query

Tất cả các phim có sự tham gia của Tom Hanks

```jsx
MATCH (tom:Person {name: "Tom Hanks"})-[:ACTED_IN]->(tomHanksMovies) RETURN tom,tomHanksMovies
```

![Untitled](Cypher%20query%20language%208d264c456286496594d409f32952da68/Untitled%201.png)

Tìm những đạo diễn của phim Cloud Atlas

```jsx
MATCH (cloudAtlas {title: "Cloud Atlas"})<-[:DIRECTED]-(directors) RETURN directors.name
```

```
╒════════════════╕
│"directors.name"│
╞════════════════╡
│"Tom Tykwer"    │
├────────────────┤
│"Andy Wachowski"│
├────────────────┤
│"Lana Wachowski"│
└────────────────┘
```

Tìm các bạn diễn của Tom Hanks

```jsx
MATCH (tom:Person {name:"Tom Hanks"})-[:ACTED_IN]->(m)<-[:ACTED_IN]-(coActors) RETURN coActors.name
```

```
╒════════════════════════╕
│"coActors.name"         │
╞════════════════════════╡
│"Parker Posey"          │
├────────────────────────┤
│"Greg Kinnear"          │
├────────────────────────┤
│"Meg Ryan"              │

.....
│"Philip Seymour Hoffman"│
├────────────────────────┤
│"Helen Hunt"            │
└────────────────────────┘
```

Mọi người có liên quan như thế nào đến phim Cloud Atlas

```jsx
MATCH (people:Person)-[relatedTo]-(:Movie {title: "Cloud Atlas"}) RETURN people.name, Type(relatedTo), relatedTo
```

```
╒══════════════════╤═════════════════╤═════════════════════════════╕
│"people.name"     │"Type(relatedTo)"│"relatedTo"                  │
╞══════════════════╪═════════════════╪═════════════════════════════╡
│"Tom Hanks"       │"ACTED_IN"       │{"roles":["Zachry","Dr. Henry│
│                  │                 │ Goose","Isaac Sachs","Dermot│
│                  │                 │ Hoggins"]}                  │
├──────────────────┼─────────────────┼─────────────────────────────┤
│"Jim Broadbent"   │"ACTED_IN"       │{"roles":["Vyvyan Ayrs","Capt│
│                  │                 │ain Molyneux","Timothy Cavend│
│                  │                 │ish"]}                       │
├──────────────────┼─────────────────┼─────────────────────────────┤
│"Tom Tykwer"      │"DIRECTED"       │{}                           │
├──────────────────┼─────────────────┼─────────────────────────────┤
│"Lana Wachowski"  │"DIRECTED"       │{}                           │
├──────────────────┼─────────────────┼─────────────────────────────┤
│"Jessica Thompson"│"REVIEWED"       │{"summary":"An amazing journe│
│                  │                 │y","rating":95}              │
├──────────────────┼─────────────────┼─────────────────────────────┤
│"Halle Berry"     │"ACTED_IN"       │{"roles":["Luisa Rey","Jocast│
│                  │                 │a Ayrs","Ovid","Meronym"]}   │
├──────────────────┼─────────────────┼─────────────────────────────┤
│"Hugo Weaving"    │"ACTED_IN"       │{"roles":["Bill Smoke","Haske│
│                  │                 │ll Moore","Tadeusz Kesselring│
│                  │                 │","Nurse Noakes","Boardman Me│
│                  │                 │phi","Old Georgie"]}         │
├──────────────────┼─────────────────┼─────────────────────────────┤
│"Andy Wachowski"  │"DIRECTED"       │{}                           │
└──────────────────┴─────────────────┴─────────────────────────────┘
```

Những bộ phim và diễn viên cách  Kevin Bacon tối đa 4 "bước nhảy"

```jsx
MATCH (bacon:Person {name:"Kevin Bacon"})-[*1..4]-(hollywood)
RETURN DISTINCT hollywood
```

![Untitled](Cypher%20query%20language%208d264c456286496594d409f32952da68/Untitled%202.png)

Con đường ngắn nhất của mối quan hệ giữa Kevin Bacon và Meg Ryan

```jsx
MATCH p=shortestPath(
(bacon:Person {name:"Kevin Bacon"})-[*]-(meg:Person {name:"Meg Ryan"})
)
RETURN p
```

![Untitled](Cypher%20query%20language%208d264c456286496594d409f32952da68/Untitled%203.png)

## 4**.Recommend**

Tìm những diễn viên mà Tom Hanks chưa hợp tác nhưng bạn diễn của anh ấy đã hợp tác:
Mở rộng các bạn diễn của Tom Hanks, để tìm những bạn diễn chưa từng làm việc với Tom Hanks ...

```jsx

[MATCH (tom:Person {name:"Tom Hanks"})-[:ACTED_IN]->(m)<-[:ACTED_IN]-(coActors),
  (coActors)-[:ACTED_IN]->(m2)<-[:ACTED_IN]-(cocoActors)
WHERE NOT (tom)-[:ACTED_IN]->()<-[:ACTED_IN]-(cocoActors) AND tom <> cocoActors
RETURN cocoActors.name AS Recommended, count(*) AS Strength ORDER BY Strength DESC
```

```
c╒════════════════════╤══════════╕
│"Recommended"       │"Strength"│
╞════════════════════╪══════════╡
│"Tom Cruise"        │5         │
├────────────────────┼──────────┤
│"Zach Grenier"      │5         │
├────────────────────┼──────────┤
│"Cuba Gooding Jr."  │4         │
├────────────────────┼──────────┤
│"Keanu Reeves"      │4         │
├────────────────────┼──────────┤
│"Jack Nicholson"    │3         │
├────────────────────┼──────────┤
│"Val Kilmer"        │3         │
├────────────────────┼──────────┤
│"Tom Skerritt"      │3         │
.....
│"Ethan Hawke"       │1         │
├────────────────────┼──────────┤
│"Rick Yune"         │1         │
├────────────────────┼──────────┤
│"Max von Sydow"     │1         │
└────────────────────┴──────────┘
```

Tìm người để giới thiệu Tom Hanks tới Tom Cruise:

```jsx
MATCH (tom:Person {name:"Tom Hanks"})-[:ACTED_IN]->(m)<-[:ACTED_IN]-(coActors),
  (coActors)-[:ACTED_IN]->(m2)<-[:ACTED_IN]-(cruise:Person {name:"Tom Cruise"})
RETURN tom, m, coActors, m2, cruise
```

![Untitled](Cypher%20query%20language%208d264c456286496594d409f32952da68/Untitled%204.png)

## 5**.Clean up**

Xóa tất cả các node và các quan hệ của nó

```jsx
MATCH (n) DETACH DELETE n
```

## 6. Chức năng của các mệnh đề trong Cypher query language

a) MATCH: so khớp với các pattern phù hợp. Đây là cách thông dụng nhất để lấy dữ liệu từ graph

Vd: Tìm tất cả các phim có đạo diễn là "Ron Howard"

```jsx
MATCH (director:Person)-[:DIRECTED]->(movie)
WHERE director.name = "Ron Howard"
RETURN movie.title
```

```
╒═══════════════════╕
│"movie.title"      │
╞═══════════════════╡
│"Frost/Nixon"      │
├───────────────────┤
│"The Da Vinci Code"│
├───────────────────┤
│"Apollo 13"        │
└───────────────────┘
```

b) WHERE: WHERE sẽ thêm các ràng buộc vào pattern, lọc tập kết quả thu được từ MATCH

c) RETURN: Mệnh đề RETURN xác định những gì cần bao gồm trong tập kết quả truy vấn, được chỉ định dưới dạng danh sách biểu thức được phân tách bằng dấu phẩy.

Vd: Trả về tất cả các đạo diễn cùng với những phim mà họ đã đạo diễn

```jsx
MATCH (director:Person)-[:DIRECTED]->(movie)
RETURN director.name AS Director, collect(movie.title) AS Movies
```

```
╒══════════════════════╤═════════════════════════════════════════════╕
│"Director"            │"Movies"                                     │
╞══════════════════════╪═════════════════════════════════════════════╡
│"Andy Wachowski"      │["Speed Racer","Cloud Atlas","The Matrix Revo│
│                      │lutions","The Matrix Reloaded","The Matrix"] │
├──────────────────────┼─────────────────────────────────────────────┤
│"Lana Wachowski"      │["Speed Racer","Cloud Atlas","The Matrix Revo│
│                      │lutions","The Matrix Reloaded","The Matrix"] │
├──────────────────────┼─────────────────────────────────────────────┤
│"Taylor Hackford"     │["The Devil's Advocate"]                     │
├──────────────────────┼─────────────────────────────────────────────┤
│"James Marshall"      │["V for Vendetta","Ninja Assassin"]          │
....
│"Milos Forman"        │["One Flew Over the Cuckoo's Nest"]          │
├──────────────────────┼─────────────────────────────────────────────┤
│"Nancy Meyers"        │["Something's Gotta Give"]                   │
├──────────────────────┼─────────────────────────────────────────────┤
│"Chris Columbus"      │["Bicentennial Man"]                         │
├──────────────────────┼─────────────────────────────────────────────┤
│"Penny Marshall"      │["A League of Their Own"]                    │
└──────────────────────┴─────────────────────────────────────────────┘
```

d) WITH: Mệnh đề WITH cho phép các truy vấn được xâu chuỗi lại với nhau, kết hợp các kết quả từ lệnh đầu tiên để được sử dụng làm điểm bắt đầu hoặc tiêu chí tiếp theo.

Vd: Tìm đường dẫn từ diễn viên triển vọng đến đạo diễn xuất sắc nhất Hollywood

```jsx
MATCH (director:Person)-[:DIRECTED]->(movie)
WITH director, count(movie) as directed ORDER BY directed DESC LIMIT 1
MATCH (hopeful:Person)-[:ACTED_IN]->(movie)
WITH director, hopeful, count(movie) as appearances ORDER BY appearances LIMIT 1
MATCH p=(director)-[*..4]-(hopeful)
RETURN p
```

![Untitled](Cypher%20query%20language%208d264c456286496594d409f32952da68/Untitled%205.png)

e) SET: Mệnh đề SET giúp cập nhật thuộc tính của node hoặc quan hệ.

Vd: Gán cho tất cả bộ phim mà Mikey xem có rating là 5

```jsx
MATCH (fan:Person)-[w:WATCHED]->(movie)
WHERE fan.name = "Mikey"
SET w.rating = 5
```

f) FOREACH: Mệnh đề FOREACH được sử dụng để cập nhật dữ liệu trong một tập hợp

Vd: 

```jsx
MATCH p = (ups)<-[DEPENDS_ON]-(device) WHERE ups.id='EPS-7001'
FOREACH (n IN nodes(p) | SET n.available = FALSE )
```

g) MERGE: Merge thực hiện chọn lọc và kiểm tra dữ liệu có tồn tại trong database hay không, trước khi chèn vào database.

Vd: Lấy ra một người tên là 'Charlie Sheen' , age là 10. Nếu không tìm được thì tạo mới như là một person

```jsx
MERGE (charlie:Person { name:'Charlie Sheen', age:10 })
RETURN charlie
```

h) CONTAINS, STARTS WITH, ENDS WITH : dùng để match string như các ngôn ngữ query khác.

i) CREATE INDEX ON : đánh index trên một thuộc tính cho tất cả các node có cùng một label

# Northwind graph

Northwind graph trình bày cách di chuyển từ cơ sở dữ liệu quan hệ sang Neo4j. Sự chuyển đổi là lặp đi lặp lại và có chủ ý, nhấn mạnh sự chuyển đổi khái niệm từ bảng quan hệ sang các node và mối quan hệ của một đồ thị.

Northwind bán một số loại thực phẩm được cung cấp bởi các nhà phân phối.

![Untitled](Cypher%20query%20language%208d264c456286496594d409f32952da68/Untitled%206.png)

Một số phần chính:

1. Load: create data from external CSV files
2. Index: index nodes based on label
3. Relate: transform foreign key references into data relationships
4. Promote: transform join records into relationships

## 1.Load csv file

Load records:

```jsx
LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/products.csv" AS row
CREATE (n:Product)
SET n = row,
n.unitPrice = toFloat(row.unitPrice),
n.unitsInStock = toInteger(row.unitsInStock), n.unitsOnOrder = toInteger(row.unitsOnOrder),
n.reorderLevel = toInteger(row.reorderLevel), n.discontinued = (row.discontinued <> "0")

LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/categories.csv" AS row
CREATE (n:Category)
SET n = row

LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/suppliers.csv" AS row
CREATE (n:Supplier)
SET n = row
```

create indexes

```jsx
CREATE INDEX ON :Product(productID)
CREATE INDEX ON :Product(productName)
CREATE INDEX ON :Product(categoryID)
CREATE INDEX ON :Product(supplierID)
```

create data relationships

```jsx
MATCH (p:Product),(c:Category)
WHERE p.categoryID = c.categoryID
CREATE (p)-[:PART_OF]->(c)
```

```jsx
MATCH (p:Product),(s:Supplier)
WHERE p.supplierID = s.supplierID
CREATE (s)-[:SUPPLIES]->(p)
```

List the product categories provide by each supplier

```jsx
MATCH (s:Supplier)-->(:Product)-->(c:Category)
RETURN s.companyName as Company, collect(distinct c.categoryName) as Categories
```

```
╒════════════════════════════════════╤════════════════════════════════════╕
│"Company"                           │"Categories"                        │
╞════════════════════════════════════╪════════════════════════════════════╡
│"Bigfoot Breweries"                 │["Beverages"]                       │
├────────────────────────────────────┼────────────────────────────────────┤
│"Exotic Liquids"                    │["Beverages","Condiments"]          │
├────────────────────────────────────┼────────────────────────────────────┤
│"Aux joyeux ecclésiastiques"        │["Beverages"]                       │
├────────────────────────────────────┼────────────────────────────────────┤
│"Leka Trading"                      │["Beverages","Condiments","Grains/Ce│
│                                    │reals"]                             │
......
│"PB Knäckebröd AB"                  │["Grains/Cereals"]                  │
├────────────────────────────────────┼────────────────────────────────────┤
│"G'day"                             │["Grains/Cereals","Meat/Poultry","Pr│
│                                    │oduce"]                             │
├────────────────────────────────────┼────────────────────────────────────┤
│"Ma Maison"                         │["Meat/Poultry"]                    │
├────────────────────────────────────┼────────────────────────────────────┤
│"Tokyo Traders"                     │["Meat/Poultry","Produce","Seafood"]│
├────────────────────────────────────┼────────────────────────────────────┤
│"New England Seafood Cannery"       │["Seafood"]                         │
├────────────────────────────────────┼────────────────────────────────────┤
│"Svensk Sjöföda AB"                 │["Seafood"]                         │
├────────────────────────────────────┼────────────────────────────────────┤
│"Escargots Nouveaux"                │["Seafood"]                         │
├────────────────────────────────────┼────────────────────────────────────┤
│"Lyngbysild"                        │["Seafood"]                         │
├────────────────────────────────────┼────────────────────────────────────┤
│"Nord-Ost-Fisch Handelsgesellschaft │["Seafood"]                         │
│mbH"                                │                                    │
└────────────────────────────────────┴────────────────────────────────────┘
```

Find the produce supplier

```jsx
MATCH (c:Category {categoryName:"Produce"})<--(:Product)<--(s:Supplier)
RETURN DISTINCT s.companyName as ProduceSuppliers
```

```
╒═══════════════════════════════════╕
│"ProduceSuppliers"                 │
╞═══════════════════════════════════╡
│"Grandma Kelly's Homestead"        │
├───────────────────────────────────┤
│"Mayumi's"                         │
├───────────────────────────────────┤
│"G'day"                            │
├───────────────────────────────────┤
│"Tokyo Traders"                    │
├───────────────────────────────────┤
│"Plutzer Lebensmittelgroßmärkte AG"│
└───────────────────────────────────┘
```

**Customer orders**

Khách hàng của Northwind có thể order chi tiết nhiều sản phẩm 

![Untitled](Cypher%20query%20language%208d264c456286496594d409f32952da68/Untitled%207.png)

Load and index records:

```jsx
LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/customers.csv" AS row
CREATE (n:Customer)
SET n = row

LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/orders.csv" AS row
CREATE (n:Order)
SET n = row

CREATE INDEX ON :Customer(customerID)

CREATE INDEX ON :Order(orderID)
```

Create data relationships

```jsx
MATCH (c:Customer),(o:Order)
WHERE c.customerID = o.customerID
CREATE (c)-[:PURCHASED]->(o)
```

Notice that Order Details are always part of an Order and that they relate the Order to a Product — they're a join table. Join tables are always a sign of a data relationship, indicating shared information between two other records.

![Untitled](Cypher%20query%20language%208d264c456286496594d409f32952da68/Untitled%208.png)

Load and index records:

```jsx
LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/order-details.csv" AS row
MATCH (p:Product), (o:Order)
WHERE p.productID = row.productID AND o.orderID = row.orderID
CREATE (o)-[details:ORDERS]->(p)
SET details = row,
details.quantity = toInteger(row.quantity)
```

Note you only need to compare property values like this when first creating relationships

### Query using patterns

```jsx
MATCH (cust:Customer)-[:PURCHASED]->(:Order)-[o:ORDERS]->(p:Product),
  (p)-[:PART_OF]->(c:Category {categoryName:"Produce"})
RETURN DISTINCT cust.contactName as CustomerName, SUM(o.quantity) AS TotalProductsPurchased
```

```
╒═════════════════════════╤════════════════════════╕
│"CustomerName"           │"TotalProductsPurchased"│
╞═════════════════════════╪════════════════════════╡
│"Elizabeth Lincoln"      │88                      │
├─────────────────────────┼────────────────────────┤
│"André Fonseca"          │62                      │
├─────────────────────────┼────────────────────────┤
│"Carlos González"        │98                      │
├─────────────────────────┼────────────────────────┤
│"Daniel Tonini"          │10                      │
.....
│"Alexander Feuer"        │40                      │
├─────────────────────────┼────────────────────────┤
│"Karl Jablonski"         │10                      │
├─────────────────────────┼────────────────────────┤
│"Matti Karttunen"        │6                       │
├─────────────────────────┼────────────────────────┤
│"Patricio Simpson"       │16                      │
├─────────────────────────┼────────────────────────┤
│"Annette Roulet"         │16                      │
└─────────────────────────┴────────────────────────┘
```